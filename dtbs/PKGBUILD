# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
_srcname=linux-5.16
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
pkgver=5.16.p1
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=21a8463b907e050953557abc796f5f235f1ba3eb
_patches=(
	'0001-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0002-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0003-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch'
	'0004-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch'
	'0005-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0006-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch'
	'0007-arm-dts-sunxi-OrangePi-Zero-improve-WiFi-bindings.patch'
	'0008-arm-dts-sunxi-OrangePi-R1-remove-correct-node.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	${_patches[@]}
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('e6680ce7c989a3efe58b51e3f3f0bf93'
         'ccb95f02e8c09a09e99ca53f3d85901e'
         'fed4f4eb458c0e970a379ac303d10565'
         '64161b2970e49dde4496a25a5238efd6'
         '3ac726179b4d772f2de6c482df6bc4a8'
         '0b598ca5c32eacf2c1f02d520a1f6395'
         '1e86097ef4cdd7c08c71e3c68ef9fac1'
         '66419743ff8522d62328a33dba51f6b6'
         'd5a695c7f9c734ad267b149f28ba0153'
         '36acf353676e9016b34cb9f866c9b10f')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun8i-h2-plus-orangepi-zero.dtb'
    'sun8i-h3-orangepi-one.dtb'
    'sun8i-h2-plus-orangepi-r1.dtb'
    'sun8i-h3-nanopi-neo.dtb'
    'sun4i-a10-olinuxino-lime.dtb'
    'sun7i-a20-olinuxino-lime.dtb'
    'sun8i-h2-plus-bananapi-m2-zero.dtb'
    'sun8i-h2-plus-bananapi-p2-zero.dtb'
  )

  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm/boot/dts/${i} "${pkgdir}/${_dstdir}"
  done
}
